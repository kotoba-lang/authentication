(ns authentication.adapters.oauth
  "Provider configuration and authorization URL construction. Token exchange,
  JWT/JWKS verification and HTTP remain host responsibilities."
  (:require [clojure.string :as str]
            [authentication.identity :as identity]))

(def provider-catalog
  {:google {:authorization-endpoint "https://accounts.google.com/o/oauth2/v2/auth"
            :token-endpoint "https://oauth2.googleapis.com/token"
            :scopes ["openid" "email" "profile"]
            :oidc? true}
   :github {:authorization-endpoint "https://github.com/login/oauth/authorize"
            :token-endpoint "https://github.com/login/oauth/access_token"
            :scopes ["read:user" "user:email"]
            :oidc? false}})

(defn- encode-component [value]
  #?(:cljs (js/encodeURIComponent (str value))
     :clj  (java.net.URLEncoder/encode (str value) "UTF-8")))

(defn authorization-url
  [{:keys [provider client-id redirect-uri state nonce code-challenge scopes]}]
  (let [{:keys [authorization-endpoint oidc?] :as config} (provider-catalog provider)]
    (when-not config (throw (ex-info "unsupported OAuth provider" {:provider provider})))
    (when-not (every? #(and (string? %) (not (str/blank? %)))
                      [client-id redirect-uri state code-challenge])
      (throw (ex-info "incomplete OAuth authorization request" {:provider provider})))
    (when (and oidc? (or (not (string? nonce)) (str/blank? nonce)))
      (throw (ex-info "OIDC nonce required" {:provider provider})))
    (let [params (cond-> [["client_id" client-id]
                          ["redirect_uri" redirect-uri]
                          ["response_type" "code"]
                          ["scope" (str/join " " (or scopes (:scopes config)))]
                          ["state" state]
                          ["code_challenge" code-challenge]
                          ["code_challenge_method" "S256"]]
                   oidc? (conj ["nonce" nonce]))]
      (str authorization-endpoint "?"
           (str/join "&" (map (fn [[k v]] (str k "=" (encode-component v))) params))))))

(defn google-profile [claims]
  (identity/normalized-profile
   {:provider :google :provider-subject (:sub claims)
    :email (:email claims) :email-verified? (:email_verified claims)
    :display-name (:name claims) :avatar-url (:picture claims)
    :claims (select-keys claims [:iss :aud :hd])}))

(defn github-profile [user primary-email]
  (identity/normalized-profile
   {:provider :github :provider-subject (str (:id user))
    :email (:email primary-email) :email-verified? (:verified primary-email)
    :display-name (or (:name user) (:login user)) :avatar-url (:avatar_url user)
    :claims {:login (:login user)}}))
