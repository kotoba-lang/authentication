(ns authentication.identity-test
  (:require [clojure.test :refer [deftest is testing]]
            [authentication.identity :as identity]))

(deftest provider-profiles-are-normalized
  (is (= {:identity/provider :google
          :identity/provider-subject "google-123"
          :identity/email "person@example.com"
          :identity/email-verified? true
          :identity/display-name "Person"
          :identity/avatar-url nil
          :identity/claims {}}
         (identity/normalized-profile
          {:provider :google :provider-subject "google-123"
           :email " Person@Example.COM " :email-verified? true
           :display-name "Person"}))))

(deftest email-collision-never-auto-links
  (let [profile (identity/normalized-profile
                 {:provider :github :provider-subject "42"
                  :email "person@example.com" :email-verified? true})]
    (is (= :require-existing-account-reauth
           (:decision (identity/link-decision
                       {:verified-email-user-id "user-1" :profile profile}))))
    (is (= :link
           (:decision (identity/link-decision
                       {:current-user-id "user-1" :profile profile}))))))

(deftest account-transaction-creates-owner-membership
  (let [profile (identity/normalized-profile
                 {:provider :email :provider-subject "mail-hash"
                  :email "person@example.com" :email-verified? true})
        tx (identity/account-tx {:user-id "u1" :tenant-id "t1"
                                 :tenant-did "did:web:kotobase.net:tenant:t1"
                                 :identity profile :now 100})]
    (is (= 4 (count tx)))
    (is (= :owner (:identity.membership/role (last tx))))
    (is (= "u1" (:identity/user (second tx))))))

(deftest session-stores-only-a-digest
  (let [session (identity/session-record
                 {:session-id "s1" :user-id "u1" :tenant-id "t1"
                  :token-digest (apply str (repeat 64 "a"))
                  :created-at 1 :expires-at 2})]
    (is (nil? (:identity.session/token session)))
    (is (= 64 (count (:identity.session/token-digest session))))))
